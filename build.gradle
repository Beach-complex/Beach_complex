import java.util.Locale;
plugins {
    id 'org.springframework.boot' version '3.3.0'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'com.diffplug.spotless' version '6.25.0'
}

group = 'com.beachcheck'
version = '0.0.1-SNAPSHOT'

// Testcontainers 버전은 Spring Boot BOM(및 dependency-management 룰)로 고정되므로,
// 필요 시 여기서 명시적으로 override 한다.
ext['testcontainers.version'] = '1.20.5'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    // TODO(OAuth): OAuth 로그인 도입 시 spring-boot-starter-oauth2-client(및 provider 설정) 추가 검토.
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'
    implementation 'org.hibernate.orm:hibernate-spatial:6.5.2.Final'
    implementation 'org.locationtech.jts:jts-core:1.19.0'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'  // 로컬 캐시 라이브러리
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'com.google.firebase:firebase-admin:9.7.0'      // Firebase Admin SDK
    implementation 'org.springframework.retry:spring-retry'        // 스프링 재시도 라이브러리
    implementation 'org.springframework.boot:spring-boot-starter-aop'   // 스프링 AOP 스타터(의존성 명시를 위해 추가)

    // Testcontainers
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'

    // 테스트용 캐시 헬퍼에서 타입 안전하게 사용 (Reflection 제거)
    testImplementation 'org.springframework:spring-context-support'  // CaffeineCache 클래스 제공
    testImplementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'

    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5'
    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

spotless{
    java {
        googleJavaFormat('1.22.0')
        target 'src/**/*.java'
    }
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // 테스트 후 리포트 자동 생성
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }
}

tasks.withType(Test).configureEach {
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    // Docker Desktop 29+는 Docker API 최소 버전이 1.44. Testcontainers 내부(shaded docker-java)의 기본값(1.41)이면
    // /v1.41/* 호출에서 400이 나며 "valid Docker environment"를 못 찾는다.
    systemProperty 'api.version', '1.44'
}


tasks.register('reservationServiceTest', Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    include("**/ReservationServiceTest.class")
}

tasks.register('installGitHooks', Exec) {
  def osName = System.getProperty('os.name').toLowerCase(Locale.ROOT)
  if (osName.contains('windows')) {
    commandLine "powershell.exe", "-ExecutionPolicy", "Bypass", "-File", "./scripts/install-githooks.ps1"
  } else {
    commandLine "sh", "./scripts/install-githooks.sh"
  }
}

checkstyle {
    toolVersion = '10.17.0'
    configFile = file('config/checkstyle/checkstyle.xml')
}
